group = 'ru.tpu.hostel'
version = '1.3.6'

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    group = 'ru.tpu.hostel'
    version = '1.3.6'

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.jetbrains:annotations:24.0.1'
        compileOnly 'org.slf4j:slf4j-api:2.0.17'
        compileOnly 'org.projectlombok:lombok:1.18.38'
        annotationProcessor 'org.projectlombok:lombok:1.18.38'
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                groupId = project.group
                artifactId = project.name
                version = project.version
            }
        }

        def repoUser = project.findProperty("privateRepoUsername") ?: System.getenv("INTERNAL_REPO_LOGIN")
        def repoPass = project.findProperty("privateRepoPassword") ?: System.getenv("INTERNAL_REPO_PASSWORD")

        repositories {
            maven {
                name = "internal"
                url = uri("http://82.202.138.26:8081/repository/internal/")
                allowInsecureProtocol = true
                credentials {
                    username = repoUser
                    password = repoPass
                }
            }
        }
    }
}

tasks.register("printVersion") {
    doLast { println project.version }
}

tasks.register("aggregateJavadoc", Copy) {
    dependsOn subprojects.collect { it.tasks.named("javadoc") }

    def outputDir = layout.projectDirectory.dir("docs/javadoc").asFile
    into outputDir

    subprojects.each { proj ->
        def javadocTask = proj.tasks.named("javadoc")
        from(javadocTask.map { it.destinationDir }) {
            into proj.name
        }
    }

    from("index.html") {
        into(".")
    }

    includeEmptyDirs = false
}
